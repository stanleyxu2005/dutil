unit dutil.sys.win32.ProcessTest;
{

 Delphi DUnit Test Case
 ----------------------
 This unit contains a skeleton test case class generated by the Test Case Wizard.
 Modify the generated code to correctly setup and call the methods from the unit
 being tested.

}

interface

uses
  TestFramework, Windows, TLHelp32, Generics.Collections, dutil.sys.win32.Process,
  SysUtils, ShellAPI;

type
  // Test methods for class TProcess

  TProcessTest = class(TTestCase)
  strict private
    FProcess: TProcess;
  public
    procedure SetUp; override;
    procedure TearDown; override;
  published
    procedure TestListAll;
    procedure TestListFilteredByProcessName;
    procedure TestFolk;
    procedure TestFolk_LessOptions;
    procedure TestTerminate;
  private
    procedure ExpectProcessCount(const ProcessName: string; Expected: Cardinal);
  end;

implementation

uses
  Forms;

procedure TProcessTest.SetUp;
begin
  FProcess := TProcess.Create;
end;

procedure TProcessTest.TearDown;
begin
  FProcess.Free;
  FProcess := nil;
end;

procedure TProcessTest.TestListAll;
var
  ReturnValue: TList<TProcessEntry32>;
begin
  ReturnValue := FProcess.ListAll;
  try
    CheckTrue(ReturnValue.Count > 2, 'At least the test runner itself and ''System Idle Process'' should exist');
  finally
    ReturnValue.Free;
  end;
end;

procedure TProcessTest.TestListFilteredByProcessName;
var
  ProcessName: string;
begin
  ProcessName := ExtractFileName(Application.ExeName);

  ExpectProcessCount(ProcessName, {Expected=}1);
end;

procedure TProcessTest.ExpectProcessCount(const ProcessName: string; Expected: Cardinal);
var
  ProcessList: TList<TProcessEntry32>;
begin
  ProcessList := FProcess.ListFilteredByProcessName(ProcessName);
  try
    CheckEquals(Expected, ProcessList.Count);
  finally
    ProcessList.Free;
  end;
end;

procedure TProcessTest.TestFolk;
var
  ShowMode: Integer;
  Masks: System.Cardinal;
  WorkingDir: string;
  Parameters: string;
  Filename: string;
begin
  Filename := '..\fixture\processtest.exe';
  Parameters := '--close';
  WorkingDir := '';
  Masks := SEE_MASK_DEFAULT;
  ShowMode := SW_HIDE;

  FProcess.Folk(Filename, Parameters, WorkingDir, Masks, ShowMode);
  ExpectProcessCount('processtest.exe', {Expected=}1);
  Windows.Sleep(50);
  ExpectProcessCount('processtest.exe', {Expected=}0);
end;

procedure TProcessTest.TestFolk_LessOptions;
var
  Parameters: string;
  Filename: string;
begin
  Filename := '..\fixture\processtest.exe';
  Parameters := '--close';

  FProcess.Folk(Filename, Parameters);
  ExpectProcessCount('processtest.exe', {Expected=}1);
  Windows.Sleep(50);
  ExpectProcessCount('processtest.exe', {Expected=}0);
end;

procedure TProcessTest.TestTerminate;
var
  Filename: string;
  Processes: TList<TProcessEntry32>;
  ExitCode: System.Cardinal;
  PID: System.Cardinal;
begin
  Filename := '..\fixture\processtest.exe';
  FProcess.Folk(Filename, {Paramters=}'');
  Processes := TProcess.ListFilteredByProcessName('processtest.exe');
  try
    CheckEquals(1, Processes.Count);
    PID := Processes[0].th32ProcessID;
  finally
    Processes.Free;
  end;
  ExitCode := 1;

  FProcess.Terminate(PID, ExitCode);
  Windows.Sleep(50);
  ExpectProcessCount('processtest.exe', {Expected=}0);
end;

initialization

// Register any test cases with the test runner
RegisterTest(TProcessTest.Suite);

end.
